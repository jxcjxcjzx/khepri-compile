/**
 * @fileOverview Khepri peephole optimization.
 */
package (
    optimize)
with
    import 'bes::record' record,
    import 'bes::array' array,

    import 'hashtrie' hashtrie,
    
    import 'khepri-ast-zipper' {khepriZipper},
    import 'neith::zipper' {detach},
    import 'neith::walk' {'walk': neithWalk},
    import 'neith::tree' tree,

    import 'khepri-ast::node' ast_node#{Node setUserData setData},
    import 'khepri-ast::declaration' ast_declaration,
    import 'khepri-ast::statement' ast_statement,
    import 'khepri-ast::expression' ast_expression,
    import 'khepri-ast::pattern' ast_pattern,
    import 'khepri-ast::value' ast_value,
    
    import 'akh::base' {
        next
        'sequence': seq
        'sequencea': seqa},
    
    import 'akh::unique' Unique,
    import 'akh::trans::state' StateT,

    import 'zipper-m::trans::zipper' ZipperT,
    import 'zipper-m::walk' walk,
    
    import './ast' {
        getUid
        isLambda
        isPrimitive
        isNumberish
        isTruthy},
    import './builtin' {builtins definitions},
    import './fun' fun#{flattenr flatten},
    import './inline/bindings' binding
in {

/* Config
 ******************************************************************************/
var MAX_EXPANSION_DEPTH := 2;

/* Forward
 ******************************************************************************/
var _check;

/* State
 ******************************************************************************/
var State := record.declare(null, [
    'bindings',
    'working',
    'globals',
    'outer',
    'ctx']);

State.empty = new State(
    binding.empty,
    binding.empty,
    hashtrie.empty,
    null,
    hashtrie.empty);

State.prototype.addBinding = \uid target =s->
    s.setBindings(
        binding.setBinding(uid, target, s.bindings));

State.prototype.addWorking = \uid target =s->
    s.setWorking(
        binding.setBinding(uid, target, s.working));

State.prototype.push = \=s->
    s.setOuter(s)
        .setWorking(binding.empty);

State.prototype.pop = \=s ->
    s.outer
        .setBindings(s.bindings)
        .setGlobals(s.globals)
        .setWorking(
            // Delete working bindings modified in inner from outer scope.
            hashtrie.fold(
                \p {key} ->
                    hashtrie.set(key, null, p),
                s.outer.working,
                s.working));

/* Monad
 ******************************************************************************/
var M = ZipperT (StateT Unique);

var run = \c ctx state seed ->
    Unique.runUnique(
        StateT.evalStateT(
            ZipperT.runZipperT(c, ctx),
            state),
        seed);

var pass = M.of null;

var unique = M.liftInner (Unique.unique);

// State Operations
////////////////////////////////////////
var getState := M.lift (M.inner.get);

var modifyState := M.lift <\ M.inner.modify;

// Movement
////////////////////////////////////////
var node =  M.node;

var modify = M.modifyNode;
var set = M.setNode;

var up = M.up;
var down = M.down;
var right = M.right;

var moveChild =  M.child;

// Bindings Operations
////////////////////////////////////////
/**
 * Register an immutable binding.
 * 
 * Immutable bindings are shared across blocks.
 * 
 * @param uid Immutable binding unique id.
 * @param value Value bound.
 */
var addBinding = \uid value ->
    modifyState \ s ->
        s.addBinding(uid, value);

/**
 * Set the working value of a binding.
 * 
 * The working value is the value of a (perhaps mutable) binding at a point in
 * execution.
 * 
 * @param uid Identifier unique id.
 * @param value Value bound.
 */
var addWorking = \uid value ->
    modifyState \ s ->
        s.addWorking(uid, value);

/**
 * 
 */
var getBinding = \uid ->
    ?uid
        :getState.map \ {bindings working}->
            (hashtrie.get(uid, working) || hashtrie.get(uid, bindings))
        :pass;
 
// Globals Operations
////////////////////////////////////////
/**
 * Get globals.
 */
var globals = getState.map \ s -> s.globals;

/**
 * Add a new global.
 */
var addGlobal = \name ->
    modifyState \ s ->
        s.setGlobals(hashtrie.set(name, name, s.globals));

// Binding operations
////////////////////////////////////////
/**
 * Enter a new scoping block.
 */
var push = modifyState \ s -> s.push();

/**
 * Exit the current the current scoping block.
 */
var pop = modifyState \ s -> s.pop();

/**
 * Evaluate `body` in a new scoping block.
 */
var block = \body(...) ->
    seq(
        push,
        seqa(body),
        pop);

/*
 ******************************************************************************/
var rewrite = \base list root ->
    neithWalk(
        \ctx ->
            let node = tree.node(ctx), uid = getUid(node) in
            ?list.indexOf(uid) >= 0
                :tree.modifyNode(\node ->
                    setData(node, 'uid', base + '-' + uid), ctx)
                :ctx,
        \x -> x,
        khepriZipper(root))
            |> tree.node;

/* Expansion
 ******************************************************************************/
var markExpansion = \id target ->
    setData(
        id,
        'expand',
        target);

var isExpansion := \node ->
    (node.ud && node.ud && node.ud.expand);

var getCtx = getState.map\s -> s.ctx;

var modifyCtx = \f ->
    modifyState \ s ->
        s.setCtx(f(s.ctx));

var expand = \exp f ->
    ?isExpansion exp
        :getCtx.chain \ ctx ->
            ?hashtrie.has(getUid exp, ctx)
                :f(exp)
                :seq(
                    modifyCtx\ctx -> hashtrie.set(getUid exp, ctx),
                    f(exp.ud.expand),
                    modifyCtx\ctx -> hashtrie.remove(getUid exp, ctx))
        :f(exp);


/*
 ******************************************************************************/
var checkTop = node.chain \ x -> _check x; // forward

var child = \args(edge ...) ->
    seq(
        moveChild edge,
        seqa([].slice.call(args, 1)),
        up);

/**
 * 
 */
var visitChild = \edge ->
    child(edge,
        checkTop);

/**
 * 
 */
var when = \test consequent alternate ->
    node.chain \ node ->
        ?test node
            :consequent
            :(alternate || pass);

/*
 ******************************************************************************/
var UP := true,
    DOWN := false;

var peepholes = {};

var addRewrite = \type f -> {
    if (Array.isArray type)
        type.forEach \ type -> addRewrite(type, f);
    else
        peepholes.(type) = f;
};

/* Operator To Function
 ******************************************************************************/
/**
 * Convert Unary Op Expression to function.
 */
addRewrite@'UnaryOperatorExpression' <|
    seq(
        node.chain \ {op} ->
            seq(
                addGlobal(op),
                set <| builtins.(op)),
        checkTop);

/**
 * Convert binary op expression to function.
 */
addRewrite@'BinaryOperatorExpression' <|
    seq(
        node.chain \ {op flipped} ->
            let name = ?flipped : '_' + op : op in
                seq(
                    addGlobal(name),
                    set <| builtins.(name)),
        checkTop);

/**
 * Convert ternary op expression to function.
 */
addRewrite@'TernaryOperatorExpression' <|
    seq(
        addGlobal('?'),
        set <| builtins.('?'),
        checkTop);

/* 
 ******************************************************************************/
// Program and Packages
addRewrite@'Program' <|
    visitChild 'body';

addRewrite@'Package' <|
    visitChild 'body';

// Clause
addRewrite@'SwitchCase' <|
    seq(
        visitChild 'test',
        visitChild 'consequent');

addRewrite@'CatchClause' <|
    seq(
        visitChild 'param',
        visitChild 'body');

// Declarations
addRewrite@'VariableDeclaration' <|
    visitChild 'declarations';

/**
 * Register constants bound in immutable variable declarations.
 * 
 * This also removes the declarations since it will be unused, unless it is the
 * only declaration of a var bound immutable to itself.
 */
addRewrite@'VariableDeclarator' <| seq(
    visitChild 'init',
    node.chain \ node ->
        ?node.init
            :?node.immutable
                :addBinding(getUid(node.id), node.init)
                :addWorking(getUid(node.id), node.init)
        :pass);

/**
 * Register constants bound in immutable variable declarations.
 * 
 * This also removes the declarations since it will be unused, unless it is the
 * only declaration of a var bound immutable to itself.
 */
addRewrite@'Binding' <| seq(
    visitChild 'value',
    
    /**
     * Register bound values.
     */
    when(
        \node ->
            (  node.pattern.type === 'IdentifierPattern'
            && getUid node.pattern.id),
        
        node.chain \ node ->
            let uid = getUid(node.pattern.id) in
                ?isPrimitive(node.value)
                    :addBinding(uid, node.value)
                    
                :?isLambda(node.value)
                    :addBinding(uid, markExpansion(node.pattern.id, node.value))
                    
                :?node.value.type === 'Identifier'
                    :getBinding(getUid node.value).chain \ binding ->
                        ?binding
                            :addBinding(uid, node.value)
                            :pass
                    
                :pass),
    
    /**
     * Expand a binding of a let expression to a set of bindings, with the top level
     * binding to the let body last.
     */
    when(
        \node ->
            (node.value.type === 'LetExpression'),
        
        node.chain \ node -> let
            bindings =
                fun.flatten <| fun.concat(
                   node.value.bindings,
                   ast_declaration.Binding.create(null,
                       node.pattern,
                       node.value.body))
       in
           seq(
               set bindings,
               visitChild(bindings.length - 1))));

// Statement
addRewrite@'BlockStatement' <|
    visitChild 'body';

addRewrite@'ExpressionStatement' <|
    visitChild 'expression';

addRewrite@'WithStatement' <|
     seq(
        visitChild 'bindings',
        visitChild 'body');

addRewrite@'SwitchStatement' <|
    seq(
        visitChild 'discriminant',
        visitChild 'cases');

addRewrite@['ReturnStatement', 'ThrowStatement'] <|
    visitChild 'argument';

addRewrite@'TryStatement' <|
    seq(
        visitChild 'block',
        visitChild 'handler',
        visitChild 'finalizer');

addRewrite@'WhileStatement' <|
    block(
        visitChild 'test',
        visitChild 'body');

addRewrite@'DoWhileStatement' <|
    block(
        visitChild 'body',
        visitChild 'test');

addRewrite@'ForStatement' <|
    seq(
        visitChild 'init',
        block(
            visitChild 'test',
            visitChild 'update',
            visitChild 'body'));

// Expression
addRewrite@'FunctionExpression' <|
    block(
         visitChild 'id',
         visitChild 'params',
         visitChild 'body');

addRewrite@'UnaryExpression' <| let
    arithmetic = {
        '!': (!),
        '~': (~),
        'typeof': (typeof),
        '++': (++),
        '--': (--)}
in
    /**
     * Combine simple unary operations on primitive literals
     */
    when(
        \{operator argument} ->
            (  arithmetic.(operator)
            && isPrimitive(argument)),
    
        modify \ {operator argument} -> let
            value = arithmetic.(operator)(argument.value)
        in
            ast_value.Literal.create(null, typeof value, value));

addRewrite@'AssignmentExpression' <| seq(
    visitChild 'right',
    
    /**
     * Update the working value of a binding.
     */
    when(
        \{left} ->
            left.type === 'Identifier',
        
        node.chain \ node ->
            addWorking(getUid(node.left), node.right)));

/**
 * Combine binary arithmetic operations on primitive literals.
 * 
 * TODO: Shortcircuit expansion for logical ops.
 */
addRewrite@['LogicalExpression', 'BinaryExpression'] <| let
    arithmetic = {
        '+': (+),
        '-': (-),
        '*': (*),
        '/': (/),
        '%': (%),
        '<<': (<<),
        '>>': (>>),
        '>>>': (>>>),
        '<': (<),
        '>': (>),
        '<=': (<=),
        '>=': (>=),
        '||': (||),
        '&&': (&&)}
in
    seq(
        visitChild 'left',
        visitChild 'right',
        when(
            \{operator left right} ->
                (  arithmetic.(operator)
                && isPrimitive(left)
                && isPrimitive(right)),
            
            modify \ {operator left right} -> let
                value = arithmetic.(operator)(left.value, right.value)
            in
                ast_value.Literal.create(null, typeof value, value)));

/**
 * Reduce conditionals with constant test.
 */
addRewrite@['ConditionalExpression', 'IfStatement'] <|
    seq(
        visitChild 'test',
        when(
            \node ->
                isPrimitive node.test,
            
            node.chain \ {test consequent alternate} ->
                seq(
                    set ?isTruthy test : consequent : alternate,
                    checkTop),
            
            seq(
                visitChild 'consequent',
                visitChild 'alternate')));

addRewrite@'MemberExpression' <|
    seq(
        visitChild 'object',
        node.chain \ node ->
            ?node.computed
                :visitChild 'property'
                :pass);

addRewrite@'NewExpression' <|
    seq(
        visitChild 'callee',
        visitChild 'args');

addRewrite@'CallExpression' <| seq(
    visitChild 'callee',
    visitChild 'args',
    
    /**
     * Inline calls of lambda functions by expanding them to let expressions.
     * 
     * The let expression is then rewritten to ensure the resulting values are unique.
     */
    when(
        \node ->
            (  isLambda(node.callee)
            || isExpansion(node.callee)
            || (node.callee.type === 'LetExpression' && isLambda(node.callee.body))),
        
        (node.chain\node -> expand(node.callee, \ callee ->
            seq(
                unique.chain \ uid ->
                modify \ node -> ?callee.type === 'Identifier': ast_expression.CallExpression.create(node.loc, callee, node.args) : let
                    target = ?callee.type === 'LetExpression' : callee.body : callee,
                    
                    map = target.params.elements.map(\x -> getUid(x.id)),
                    
                    bindings =
                        target.params.elements.map \ x i ->
                            ast_declaration.Binding.create(null,
                                x,
                                ?node.args.(i)
                                    :node.args.(i)
                                    :ast_value.Identifier.create(null, 'undefined'))
                in
                    rewrite(uid, map, ast_expression.LetExpression.create(null,
                        fun.concat(
                            callee.bindings || [],
                            bindings),
                        target.body)),
             ?callee.type === 'Identifier' :pass: checkTop)))));

addRewrite@'CurryExpression' <| seq(
    visitChild 'base',
    visitChild 'args',
    
    /**
     * Convert curry on function expression to function.
     * 
     * Discards arguments that are not used.
     */
    when(
        \node ->
            (  isLambda(node.base)
            || (node.base.type === 'LetExpression' && isLambda(node.base.body))),

        seq(
            unique.chain \ uid ->
            modify \ node -> let
                target = ?node.base.type === 'LetExpression' : node.base.body : node.base
            in
                ?!target.params.elements.length
                    :node.base
                    :let
                        first = target.params.elements.(0),
                        rest = target.params.elements.slice(1),
                        
                        map = [getUid(first.id)],
                        
                        body = ast_expression.FunctionExpression.create(null, null,
                            ast_pattern.ArgumentsPattern.create(null, null,
                                rest,
                                target.params.self),
                            rewrite(uid, map, target.body))
                    in
                        ?first && (first.type === 'IdentifierPattern' || first.type === 'AsPattern' || first.type === 'ObjectPattern')
                            :ast_expression.LetExpression.create(null,
                                fun.concat(
                                    node.base.bindings || [],
                                    rewrite(uid, map, ast_declaration.Binding.create(null,
                                        first,
                                        node.args.(0)))),
                                body)
                            :body,
                checkTop)));

addRewrite@'LetExpression' <| seq(
    visitChild 'bindings',
    visitChild 'body',
    
    /**
     * Flatten let expression bindings.
     */
    modify \ {loc bindings body} ->
        ast_expression.LetExpression.create(loc,
            flattenr(bindings),
            body),
    /**
     * Collapse empty let expressions.
     */
    when(
        \{bindings} ->
            !bindings.length,
        
        modify \ {body} ->
            body));

// Value
addRewrite@'ArrayExpression' <|
    visitChild 'elements';

addRewrite@'ObjectExpression' <|
    visitChild 'properties';

addRewrite@'ObjectValue' <|
    visitChild 'value';

addRewrite@'Identifier' <|
    when(
        \node -> getUid node,
        
        node.chain \ node ->
            getBinding(getUid node).chain \ binding ->
                ?binding && (isPrimitive(binding) || binding.type === 'Identifier' || isLambda(binding))
                    :set(binding)
                    :pass);

/* Interface
 ******************************************************************************/
_check = \node -> {
    if (Array.isArray(node)) {
        if (!node.length)
            return pass;
        
        return seq(
            down,
            seqa(node.map(\_ i -> 
                ?i === node.length - 1
                    :checkTop
                    :next(checkTop, right))),
            up);
    }
    
    if (node instanceof ast_node.Node && peepholes.(node.type))
        return peepholes.(node.type);
    
    return pass;
};

var initialState = fun.foldl(
    \s name -> let
        id = builtins.(name),
        def = definitions.(name)
    in
        s.setBindings(
            hashtrie.set(
                getUid id,
                markExpansion(id, def),
                s.bindings)),
    State.empty,
    Object.keys(builtins));

/**
 * 
 */
optimize = \ast data ->
    run(
        next(
            checkTop,
            node.chain \ node ->
            globals.chain \ g ->
            unique.chain \ unique ->
                M.of({
                    'tree': node,
                    'data': {'globals': hashtrie.keys(g), 'unique': unique}
                })),
        khepriZipper(ast),
        initialState,
        data.unique);

}
