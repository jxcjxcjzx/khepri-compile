/**
 * @fileOverview Khepri peephole optimization.
 */
package (
    optimize)
with
    import 'khepri-ast-zipper' {khepriZipper},

    import 'khepri-ast::node' {Node setUserData setData},
    import 'khepri-ast::declaration' ast_declaration,
    import 'khepri-ast::statement' ast_statement,
    import 'khepri-ast::expression' ast_expression,
    import 'khepri-ast::value' ast_value,
    
    import 'akh::base' {
        next
        'sequence': seq
        'sequencea': seqa},
    
    import 'akh::unique' Unique,

    import 'zipper-m::trans::zipper' ZipperT,
    import 'zipper-m::walk' walk,
    
    import './fun' fun,
    import './ast' {
        isNumberish
        getUid}
in {

/* Operations
 ******************************************************************************/
var M = ZipperT (Unique);

var run = \c ctx seed ->
    Unique.runUnique(
        ZipperT.runZipperT(c, ctx),
        seed);

var pass = M.of null;

var node =  M.node;
var modify = M.modifyNode;
var set = M.setNode;

var unique = M.liftInner (Unique.unique);

/*
 ******************************************************************************/
var peepholes = {};

var addPeephole = \types, up, condition, f -> {
    var entry = {
        'condition': condition,
        'map': f,
        'up': up
    };
    
    types.forEach \ type -> {
        peepholes.(type) = ?peepholes.(type)
            :fun.concat(peepholes.(type), entry)
            :[entry];
    };
};

/* Binding Expression Normalization
 ******************************************************************************/
/**s
 * Flatten variable declaration declaration lists and remove if declaration list
 * is empty.
 */
addPeephole(['VariableDeclaration'], true,
    \_ -> true,
    
    modify \ {declarations} ->
        let bound = fun.flattenr <| declarations in
            ?bound.length
                :ast_declaration.VariableDeclaration.create(null, bound)
                :null);

/**
 * Flatten let expression bindings lists and collapse to body 
 * when binding list is empty
 */
addPeephole(['LetExpression'], true,
    \_ -> true,
    
    modify \ {bindings body} ->
        let bound = fun.flattenr <| bindings in
            ?bound.length
                :ast_expression.LetExpression.create(null, bound, body)
                :body);

/**
 * Flatten with statement bindings lists and collapse to body 
 * when binding list is empty.
 */
addPeephole(['WithStatement'], true,
    \_ -> true,
    
    modify \ {bindings body} ->
        let bound = fun.flattenr <| bindings in
            ?bound.length
                :ast_statement.WithStatement.create(null, bound, body)
                :body);

/* Flattening
 ******************************************************************************/
/**
 * Flatten let with let body.
 */
addPeephole(['LetExpression'], true,
    \node ->
        node.body.type === 'LetExpression',
    modify \ node ->
        ?node.body.type === 'LetExpression'
            :ast_expression.LetExpression.create(null,
                fun.concat(
                    node.bindings,
                    node.body.bindings),
                node.body.body)
            :node);

/**
 * Flatten curry of curry.
 */
addPeephole(['CurryExpression'], true,
    \node ->
        node.base.type === 'CurryExpression',
    modify\node ->
        ast_expression.CurryExpression.create(null,
            node.base.base,
            fun.concat(
                node.base.args,
                node.args)));

/* Let to With Statement Rewrites
 ******************************************************************************/
/**
 * Rewrite `return let` to a with statement.
 */
addPeephole(['ReturnStatement'], false,
    \node ->
        (node.argument && node.argument.type === 'LetExpression'),
    modify\node ->
        ast_statement.WithStatement.create(null,
            node.argument.bindings,
            ast_statement.BlockStatement.create(null, 
                [ast_statement.ReturnStatement.create(node.loc, node.argument.body)])));

/**
 * Rewrite `\...-> let` to a with statement.
 */
addPeephole(['FunctionExpression'], false,
    \node ->
        (node.body.type === 'LetExpression'),
    modify\node ->
        ast_expression.FunctionExpression.create(null,
            node.id,
            node.params,
            ast_statement.BlockStatement.create(null, [
                ast_statement.WithStatement.create(null,
                    node.body.bindings,
                    ast_statement.BlockStatement.create(null, 
                        [ast_statement.ReturnStatement.create(node.loc, node.body.body)]))])));

/**
 * Rewrite top level let expressions to with statements.
 */
addPeephole(['ExpressionStatement'], true,
    \node ->
        (node.expression && node.expression.type === 'LetExpression'),
    modify\node ->
        ast_statement.WithStatement.create(null,
            node.expression.bindings,
            ast_statement.BlockStatement.create(null, 
                [ast_statement.ExpressionStatement.create(node.loc, node.expression.body)])));

/**
 * Rewrite top level let expressions to with statements.
 */
addPeephole(['ExpressionStatement'], true,
    \node ->
        (  node.expression
        && node.expression.type === 'AssignmentExpression'
        && node.expression.right.type === 'LetExpression'),
    modify\node ->
        ast_statement.WithStatement.create(null,
            node.expression.right.bindings,
            ast_statement.BlockStatement.create(null, 
                [ast_statement.ExpressionStatement.create(node.loc,
                    ast_expression.AssignmentExpression.create(node.expression.loc, node.expression.operator,
                        node.expression.left,
                        node.expression.right.body))])));

/* Interface
 ******************************************************************************/
var upTransforms = \node -> 
    ((node && peepholes.(node.type)) || [])
        .filter(\x -> x.up && x.condition(node));

var downTransforms = \node -> 
    ((node && peepholes.(node.type)) || [])
        .filter(\x -> !x.up && x.condition(node));

var transform = \node transforms ->
    ?transforms.length
        :seqa(transforms.map(\x -> x.map))
        :pass;

var _transform = node.chain \ node -> 
    transform(node, downTransforms node);

var _transformPost = node.chain \ node ->
    transform(node, upTransforms node);

/**
 * 
 */
optimize = \ast data ->
    run(
        next(
            walk(M, _transform, _transformPost),
            node),
        khepriZipper(ast),
        data.unique);

}
