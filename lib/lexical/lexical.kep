/**
 * @fileOverview Lexical checking of an AST and tagging of identifiers with unique
 * ids.
 */
package (
    check)
with
    import 'akh::base' {
        next
        'sequence' # seq
        'sequencea' # seqa},
    import 'akh::trans::statei' StateT,
    import 'akh::error' Error,
    import 'akh::trans::error' ErrorT,
    import 'akh::unique' Unique,
    
    import 'khepri-ast::expression' ast_expression,
    import 'khepri-ast::value' ast_value,

    import 'zipper-m::trans::tree' TreeZipperT,
    
    import '../ast' {
        type
        isSymbol
        getUd
        setUd
        setUid
        setLocals
        setClosure},
    import '../fun' {
        flip
        foldl
        map
        notContains
        arrayDiff},
    import '../user_operator' {splitOp},
    import './scope' scope#{Scope},
    import './scopet' ScopeT
in {

var _check; // forward


var reserved := getUd @ 'reserved';

var markUserDefined := setUd @ 'userDefined' @ true;

var getStart := (_ ?? .start);

/* Monad
 ******************************************************************************/
var M = ErrorT (TreeZipperT (ScopeT (Unique)));

var run = \p s ctx ok err->
    Unique.runUnique(
        StateT.evalStateT(
            TreeZipperT.runTreeZipperT(
                ErrorT.runErrorT(
                    p,
                    ok \> M.inner.of,
                    err \> M.inner.of),
            ctx),
            s),
        1000);

var pass = M.of null;

var error = M.fail;

var lift = M.lift;

var unique = M.chain @ (M.liftInner.liftInner (Unique.unique));

// Scope Operations
////////////////////////////////////////
var extractScope = M.liftInner (M.inner.inner.extractScope);

var examineScope = M.chain @ extractScope;

var modifyScope = M.liftInner <\ M.inner.inner.modify;

var push = M.liftInner (M.inner.inner.push);
var pop = M.liftInner (M.inner.inner.pop);

var getLocals = M.chain @ (M.liftInner (M.inner.inner.getLocals));
var getClosure = M.chain @ (M.liftInner (M.inner.inner.getClosure));

// Movement Operations
////////////////////////////////////////
var extractCtx := lift (M.inner.get);

var extract := lift (M.inner.node);

var inspect := M.chain @ extract;

var up := lift (M.inner.up);
var down := lift (M.inner.down);
var left := lift (M.inner.left);
var right := lift (M.inner.right);

var root := lift (M.inner.root);

var moveChild :=  lift <\ M.inner.child;

var modifyNode := lift <\ M.inner.modifyNode;
var setNode := lift <\ M.inner.setNode;

/**
 * Move along `edge` and perform `actions`.
 * 
 * Returns to current node.
 */
var child := \edge ...actions ->
    seq(
        moveChild edge,
        seqa actions,
        up);

/**
 * Check the current node.
 */
var checkTop := inspect \ x -> _check x; // forward

/**
 * Move to child `edge` and check it.
 * 
 * Returns to current node.
 */
var checkChild := \edge ->
    child(edge,
        checkTop);

/**
 * Check all children of an array node.
 */
var checkArray := let
    visit = \_ i arr -> 
        ?i === arr.length - 1
            :checkTop
            :next(checkTop, right)
in
    \arr ->
        ?arr.length
            :seq(
                down,
                seqa arr.map(visit),
                up)
        :pass;

/* Operations
 ******************************************************************************/
/**
 * When `test` is true for current node, evaluate consequent. Otherwise evaluate
 * alternate.
 */
var when = \test consequent alternate ->
    inspect \ node ->
        ?test node
            :consequent
            :(alternate || pass);

/**
 * Create a new lexical block for body.
 */
var block = \-body(...)->
    seq(
        push,
        seqa body,
        pop);

/**
 * Define a new identifier and create a uid entry for it.
 */
var addUid = \id ->
    unique \ uid ->
        modifyScope (scope.addUid@(id, uid));

/**
 * Get the uid of an identifier.
 */
var getUid = \id ->
    extractScope.map (scope.getUid @ id);

var addRef = \id ->
    modifyScope (scope.addRef @ id);

/**
 * Ensure the current scope contains a binding for `id`.
 */
var checkHasBinding = \id, loc ->
    examineScope \ s ->
        ?scope.hasBinding(id, s)
            :pass
        :error("Undeclared symbol:'" + id + "' at:" + getStart loc);

/**
 * Ensure the current scope does not have a binding for `id`.
 */
var checkCanAddBinding = \id loc -> 
    examineScope \ s ->
        ?s.hasOwnBinding(id)
            :let binding = s.getBinding(id) in
                error("'" + id + "' at:" + getStart loc + " already bound for scope from:" + getStart binding.loc)
        :pass;

/**
 * Ensure `id` is a mutable binding.
 */
var checkCanAssign = \id, loc -> 
    examineScope \ s ->
        ?scope.hasMutableBinding(id, s)
            :pass
        :error("Assign to immutable symbol:'" + id + "' at:" + getStart loc);

/**
 * Mark existing binding for `id` as immutable.
 * 
 * This must take place in the same scope the variable was declared in.
 */
var markBindingImmutable = \id loc ->
    examineScope \ s ->
        ?s.hasOwnBinding id
            :?scope.hasTransMutableBinding(id, s)
                :modifyScope (scope.markBindingImmutable @ id)
                :error("Cannot mark symbol:'" + id + "' at:" + getStart loc + ' as it was mutated in an enclosed scope')
        :error("Cannot mark symbol:'" + id + "' at:" + getStart loc + ' immutable in enclosed scope');

/**
 * Mark existing binding for `id` as mutable.
 * 
 * When this takes place in an inner scope, the binding is locked into a mutable
 * state and cannot be declared immutable after this.
 */
var markBindingMutable = \id loc ->
    examineScope \ s ->
        modifyScope (scope.markBindingMutable@(id, s.hasOwnBinding id));

/**
 * Add a new mutable binding in the current scope.
 * Does not perform any checks.
 * 
 * All mutable bindings are initially trans mutable.
 */
var addMutableBinding = \id loc -> 
    seq(
         modifyScope (scope.addTransMutableBinding@(id, loc)),
         addUid id);

/**
 * Adds a new static binding in the current scope.
 * 
 * Static binds are immutable and are not effected by renaming.
 * 
 * Does not perform any checks.
 */
var addStaticBinding = \id loc -> 
     modifyScope (scope.addImmutableBinding@(id, loc));

/**
 * Adds a new immutable binding in the current scope.
 * Does not perform any checks.
 */
var addImmutableBinding = \id loc -> 
    seq(
         addStaticBinding(id, loc),
         addUid id);

/**
 * Add a new mutable binding in the current scope.
 * Checks to make sure the new binding is valid.
 */
var addMutableBindingChecked = \id loc ->
    seq(
        checkCanAddBinding(id, loc),
        addMutableBinding(id, loc));

/**
 * Add a new immutable binding in the current scope.
 * Checks to make sure the new binding is valid.
 */
var addImmutableBindingChecked = \id loc ->
    seq(
        checkCanAddBinding(id, loc),
        addImmutableBinding(id, loc));

/**
 * Add a new immutable binding in the current scope.
 * Checks to make sure the new binding is valid.
 */
var addStaticBindingChecked = \id loc ->
    seq(
        checkCanAddBinding(id, loc),
        addStaticBinding(id, loc));

/**
 * Split symbol `name` according to set of valid unary operators in current scope.
 */
var splitUnary = let
    createOp = \[op uid] -> 
        setUid(uid, ast_value.UnaryOperator.create(null, op))
in
    \name ->
        extractScope.map \ s ->
            map(
                createOp,
                splitOp(name, scope.getOperators(s)));

/**
 * Add a unary operator in the current scope
 */
var addOperator = \name uid-> 
    modifyScope (scope.addOperator@(name, uid));

/* Inner Checks
 ******************************************************************************/
var checkBlockSameScope = let
    checkWith = seq(
        checkChild 'bindings',
        child('body',
            checkChild 'body')),
    
    checkBlock = checkChild 'body'
    
in
    \alt ->
        inspect \ node ->
            ?type node === 'WithStatement'
                :checkWith
            :?type node === 'BlockStatement'
                :checkBlock
            :alt;
    
/* Checks
 ******************************************************************************/
var checks = { };

var addCheck = \type, check -> {
    if (Array.isArray(type))
        type.forEach \ x -> addCheck(x, check);
    else
        checks.(type) = check;
};

// Program and Packages
addCheck@'Program' <|
    block(
        checkChild 'body');

addCheck@'PackageExports' <|
    checkChild 'exports';

addCheck@'PackageExport' <| seq(
    inspect \ {id} ->
        addMutableBindingChecked(id.name, id.loc),
    checkChild 'id');

addCheck@'Package' <|
    block(
        checkChild 'exports',
        // Evaluate body in same scope
        child('body',
            checkBlockSameScope(checkChild 'body')));

// Declarations
addCheck@['StaticDeclaration', 'VariableDeclaration'] <|
    checkChild 'declarations';

addCheck@'StaticDeclarator' <|
    inspect \ {id} ->
        addStaticBindingChecked(id.name, id.loc);

addCheck@'VariableDeclarator' <| let
    bind = \node ->
        ?node.immutable
            :addImmutableBindingChecked(node.id.name, node.loc)
            :addMutableBindingChecked(node.id.name, node.loc)
in
    when(.recursive,
        seq(
            inspect bind,
            checkChild 'id',
            checkChild 'init'),
        seq(
            checkChild 'init',
            inspect bind,
            checkChild 'id'));

addCheck@'Binding' <|
    when(.recursive,
        seq(
            checkChild 'pattern',
            checkChild 'value'),
        seq(
            checkChild 'value',
            checkChild 'pattern'));

// Statement
addCheck@'BlockStatement' <|
    block(
        checkChild 'body');

addCheck@'ExpressionStatement' <|
    checkChild 'expression';

addCheck@'IfStatement' <| seq(
    checkChild 'test',
    block(
        checkChild 'consequent'),
    block(
        checkChild 'alternate'));

addCheck@'WithStatement' <|
    block(
        checkChild 'bindings',
        child('body',
            checkChild 'body'));

addCheck@'SwitchStatement' <|
    block(
        checkChild 'discriminant',
        checkChild 'cases');

addCheck@'SwitchCase' <| seq(
    checkChild 'test',
    checkChild 'consequent');

addCheck@['ReturnStatement', 'ThrowStatement'] <|
    checkChild 'argument';

addCheck@'TryStatement' <| seq(
    checkChild 'block',
    block(
        checkChild 'handler'),
    block(
        checkChild 'finalizer'));

addCheck@'CatchClause' <|
    block(
        inspect \ {param} ->
            addImmutableBindingChecked(param.name, param.loc),
        checkChild 'param',
        child('body',
            checkChild 'body'));

addCheck@'WhileStatement' <| seq(
    checkChild 'test',
    block(
        checkChild 'body'));

addCheck@'DoWhileStatement' <| seq(
    block(
        checkChild 'body'),
    checkChild 'test');

addCheck@'ForStatement' <|
    block(
        checkChild 'init',
        checkChild 'test',
        checkChild 'update',
        block(
            checkChild 'body'));

// Expression
addCheck@'FunctionExpression' <|
     getClosure \ parentClosure ->block(
        when(.id,
            seq(
                inspect \ {id} ->
                    addImmutableBinding(id.name, id.loc),
                checkChild 'id')),
        
        // Get closure data.
        // This must include the function's id
        getLocals \ closure -> seq(
            
            checkChild 'params',
            getClosure \ params -> seq(
            
            // Evaluate body in same scope
            child('body',
                checkBlockSameScope(checkTop)),
        
        // Attach locals data
        getClosure \ c ->
        getLocals \ locals ->
        modifyNode \node ->
            node
                |> setLocals @ arrayDiff(locals, closure)
                |> setClosure @ arrayDiff(c, params))));

addCheck@'UnaryExpression' <| seq(
    checkChild 'argument',
    
    inspect \ {loc operator argument} ->
        splitUnary(operator.name).chain \ ops ->
            ?ops.length > 1
                :seq(
                    setNode <| ops.reduceRight(\p c ->
                        ast_expression.UnaryExpression.create(loc, c, p),
                        argument),
                    checkTop)
                :checkChild 'operator');

addCheck@'AssignmentExpression' <| seq(
    checkChild 'left',
    when(.left \> isSymbol,
        inspect \ {immutable left#{name loc}} -> seq(
            checkCanAssign(name, loc),
            ?immutable
                :markBindingImmutable(name, loc)
                :markBindingMutable(name, loc))),
    checkChild 'right');

addCheck@'DeleteExpression' <|
    checkChild 'argument';

addCheck@'BinaryExpression' <| seq(
    checkChild 'operator',
    checkChild 'left',
    checkChild 'right');

addCheck@'ConditionalExpression' <| seq(
    checkChild 'test',
    checkChild 'consequent',
    checkChild 'alternate');

addCheck@['CallExpression', 'NewExpression'] <| seq(
    checkChild 'callee',
    checkChild 'args');

addCheck@['MemberExpression', 'CheckedMemberExpression'] <| seq(
    checkChild 'object',
    when(.computed,
        checkChild 'property'));

addCheck@'ArrayExpression' <|
    checkChild 'elements';

addCheck@'ObjectExpression' <|
    checkChild 'properties';

addCheck@'LetExpression' <|
    block(
        checkChild 'bindings',
        checkChild 'body');

addCheck@'CurryExpression' <| seq(
    checkChild 'base',
    checkChild 'args');

addCheck@'OperatorExpression' <|
    checkChild 'operator';

// Patterns
addCheck@'EllipsisPattern' <|
    checkChild 'id';

addCheck@['SliceUnpack', 'RelativeUnpack', 'ImportPattern'] <|
    checkChild 'pattern';

addCheck@'IdentifierPattern' <| seq(
    inspect \ node#{loc id} ->
        ?reserved node
            :addImmutableBinding(id.name, loc)
            :addImmutableBindingChecked(id.name, loc),
    checkChild 'id');

addCheck@'AsPattern' <| seq(
    checkChild 'id',
    inspect \ node ->
        child('target',
            modifyNode \ target ->
                // Hack: For generating argument lists, we want to use the 
                // as pattern name if possible.
                setUd('id', node.id, target),
            checkTop));

addCheck@'ObjectPattern' <|
    checkChild 'elements';

addCheck@'ObjectPatternElement' <| seq(
    checkChild 'target',
    checkChild 'key');

addCheck@'ArgumentsPattern' <| seq(
    checkChild 'id',
    checkChild 'elements',
    checkChild 'self');

// Value
addCheck@'ObjectValue' <|
    checkChild 'value';

addCheck@['Identifier', 'BinaryOperator'] <|
    inspect \ node#{loc name} -> seq(
        checkHasBinding(name, loc),
        examineScope \ s -> let
            uid = scope.getUid(name, s)
        in seq(
            addRef uid,
            setNode <| setUid(uid, markUserDefined node)));

addCheck@['UnaryOperator'] <|
    inspect \ node#{loc name} -> seq(
        checkHasBinding(name, loc),
        examineScope \ s ->
            let uid = scope.getUid(name, s) in seq(
                setNode <| setUid(uid, markUserDefined node),
                addOperator(name, uid)));

/*
 ******************************************************************************/
_check := \node ->
    ?Array.isArray node
        :checkArray node
        :(checks.(type node) || pass);

var addGlobals := flip foldl @ \s c ->
    scope.addImmutableBinding(c, 'global', s);

var addUnaryOps := flip foldl @ \s c ->
    scope.addOperator(c, 'global',
        scope.addImmutableBinding(c, 'global', s));

var rewrite := seq(
    checkTop,
    root,
    extractCtx.chain \ x ->
    unique \ unique ->
    extractScope.map \ s -> ({
        tree: x,
        data: {
            unique: unique
        }
    }));

/**
 * Lexical check `ast` and rewrite it by taging every identifier with a unique id.
 * 
 * Returns result in `Error`.
 * 
 * @param ast Zippered khepri AST.
 * @param globals List of global identifiers.
 * @param builtinBinary List of global binary operators.
 * @param builtinUnary List of global unary operators.
 */
check = \ast globals builtinBinary builtinUnary ->
    run(
        rewrite,
        Scope.empty
            |> addGlobals@(globals || [])
            |> addGlobals@(builtinBinary || [])
            |> addUnaryOps@(builtinUnary || []),
        ast,
        Error.of,
        Error.fail);

}