/**
 * @fileOverview 
 */
package (
    Scope

// Binding Ops
    hasBinding
    hasMutableBinding
    
    addBinding
    addMutableBinding
    addImmutableBinding
    setBindingMutability
    
// Definition Ops
    getUid
    addUid
    
// Mapping Ops
    hasMapping
    hasOwnMapping
    getMapping
    addMapping
    
    getClosure
    
    addOperator
    getOperators
    
    push
    pop
    
    addVar)
with
    import 'bes::record' record,
    
    import 'hamt' hamt
in {

var IMMUTABLE := 0,
    MUTABLE := 1,
    TRANS := 2;

/* Scope
 ******************************************************************************/
/**
 * Holds information about a lexical scope.
 * 
 * @member record Maps identifiers to binding information for this scope.
 * @member outer Parent scope.
 * @member mapping Maps identifiers to their actual names in this scope.
 * @member definitions Maps identifiers to unique ids.
 * @member locals Set of all unique ids.
 * @member locals Map of all valid prefix operator names to uids in the scope.
 */
Scope = record.declare(null, [
    'record',
    'outer',
    'mapping',
    'definitions',
    'locals',
    'prefixOps']);

Scope.empty = Scope.create(
    hamt.empty,
    null,
    hamt.empty,
    hamt.empty,
    hamt.empty,
    hamt.empty);

/**
 * Does this scope have a binding for `id`?
 */
Scope.prototype.hasOwnBinding = \id =self->
    hamt.has(id, self.record);

/* Binding Operations
 ******************************************************************************/
/**
 * Does scope `s` or its parents have a given binding for `id`?
 */
hasBinding := \id s->
    (  s.hasOwnBinding(id)
    || (s.outer && hasBinding(id, s.outer)));

/**
 * Does scope `s` or its parents have a mutable binding for `id`?
 */
hasMutableBinding := \id s -> let
    binding = s.getBinding(id)
in
    (binding && binding.mutable);

/**
 * Get information for a given binding.
 * 
 * Returns null if binding does not exist.
 */
Scope.prototype.getBinding = \id =self->
    (  hamt.get(id, self.record)
    || ?self.outer
        :self.outer.getBinding(id)
        :null);

/**
 * Get an unused identifier in this scope.
 */
Scope.prototype.getUnusedId = \id =self-> {
    if (!hasBinding(id, self))
        return id;
    
    for (var i = 0; ; i = i + 1)
        if (!hasBinding(id + i, self))
            return id + i;
};

/**
 * Create a new modified scope with a given binding.
 * 
 * @param s Scope.
 * @param id Identifier for new binding.
 * @param info Binding information.
 */
addBinding = \s id info -> 
    s.setRecord <|
        hamt.set(id, info, s.record);

/**
 * Create a new modified scope with a new mutable binding.
 * 
 * @param id Identifier for new binding
 * @param loc Location of binding in source code.
 * @param s Scope
 */
addMutableBinding := \id loc s ->
    addBinding(s, id, {
        mutable: MUTABLE,
        loc: loc
    });

/**
 * Create a new modified scope with a new immutable binding.
 * 
 * @param id Identifier for new binding
 * @param loc Location of binding in source code.
 * @param s Scope
 */
addImmutableBinding = \id loc s ->
    addBinding(s, id, {
        mutable: IMMUTABLE,
        loc: loc
    });

/**
 * Create a new modified scope with a new immutable binding.
 * 
 * @param id Identifier for new binding
 * @param loc Location of binding in source code.
 * @param s Scope
 */
setBindingMutability = \id mutable s ->
    ?s.hasOwnBinding id
        :addBinding(s, id, {
            loc: s.getBinding(id).loc,
            mutable: ?mutable : MUTABLE : IMMUTABLE
        })
        :(s.outer && s.setOuter(setBindingMutability(id, mutable, s.outer)));

/* Definition Operations
 ******************************************************************************/
/**
 * Get the unique identifier for `id`.
 * 
 * @param id Identifier.
 * @param s SCope.
 */
getUid := \id s ->
    ?s.hasOwnBinding(id)
        :hamt.get(id, s.definitions)
        :(s.outer && getUid(id, s.outer));

/**
 * Register a unique identifier in scope `s`.
 * 
 * @param id Identifier
 * @param uid Unique identifier.
 * @param s Scope.
 */
addUid = \id uid s ->
    s
        .setDefinitions(
            hamt.set(id, uid, s.definitions))
        .setLocals(
            hamt.set(uid, id, s.locals));

/* Mapping Operations
 ******************************************************************************/
/**
 * Does this scope have its own a mapping for a given identifier.
 */
Scope.prototype.hasOwnMapping = \id =self->
    hamt.has(id, self.mapping);

/**
 * Does this scope have any mapping for a given identifier.
 */
Scope.prototype.hasMapping = \id =self->
    (  self.hasOwnMapping(id)
    || (self.outer && self.outer.hasMapping(id)));

/**
 * Get the mapped value for `id` in scope `s`.
 */
getMapping := \id s ->
    ?s.hasOwnMapping(id)
        :hamt.get(id, s.mapping)
        :s.outer && getMapping(id, s.outer);

/**
 * Create a new modified scope with a new mapping
 * 
 * @param from Key value.
 * @param to Value to map to.
 * @param s Scope
 */
addMapping = \from to s ->
    s.setMapping <|
        hamt.set(from, to, s.mapping);

/**
 * Add a new identifier to scope `s`.
 * 
 * @param id Source id name.
 * @param uid Unique id.
 * @param s Scope.
 */
addVar = \id uid s ->
    ?s.hasMapping(uid)
        :addMutableBinding(id, null, s)
    
    :let name = s.getUnusedId(id) in
        addMapping(
            uid,
            name,
            addMutableBinding(name, null, s));

/* Operations
 ******************************************************************************/
/**
 * Get a list of all valid uids in the current scope.
 * 
 * @param s Scope.
 */
getClosure := (.locals) \> hamt.keys;

/**
 * Enter a new empty scope with its parent set to `s`.
 * 
 * @param s Scope.
 */
push = \s ->
    Scope.empty
        .setOuter(s)
        .setDefinitions(s.definitions)
        .setPrefixOps(s.prefixOps);

/**
 * Pop scope.
 * 
 * @param s Scope.
 */
pop = let
    mergeLocals =
        hamt.fold @ \p {key value} -> hamt.set(key, value, p)
in
    \s ->
        s.outer
            .setLocals(
                mergeLocals(s.outer.locals, s.locals));

/* Operator Operations
 ******************************************************************************/
addOperator := \name uid s ->
    s.setPrefixOps <|
        hamt.set(name, uid, s.prefixOps);

getOperators := (.prefixOps) \> hamt.pairs;

}