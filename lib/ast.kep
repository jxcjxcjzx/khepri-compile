/**
 * @fileOverview Khepri AST nodes predicates.
 */
package (
    type
    
    isIdentifier
    isOperator
    isSymbol
    
    isLiteral
    isNumberish
    isPrimitive
    isSimple
    isPod
    
    isTruthy
    
    isBlockFunction
    isLambda
    isLambdaWithoutArgs

    tryGetUd
    getUd
    setUd
    
    modify
    
    getUid
    setUid
    
    getLocals
    setLocals
    getClosure
    setClosure)
with
    import 'khepri-ast::node' {
        setData
        'modify': modifyAstNode}
in {

/**
 * Safe node type getter
 */
type := (_ ?? .type);

/* Symbols
 ******************************************************************************/
/**
 * Is an AST node an identifier?
 */
isIdentifier := type \> (=== 'Identifier');

/**
 * Is an AST node an operator symbol?
 */
isOperator := \node ->
    (  type node === 'UnaryOperator'
    || type node === 'BinaryOperator'
    || type node === 'TernaryOperator');

/**
 * Is an AST node a symbol?
 */
isSymbol := \node ->
    (  isIdentifier node
    || isOperator node);

/* Literals
 ******************************************************************************/
/**
 * Is a node a literal.
 * 
 * This includes regular expressions.
 */
isLiteral = type \> (=== 'Literal');

/**
 * Can a node be treated as a number?
 */
isNumberish = \node ->
    (  isPrimitive node
    && !isNaN node.value);

/**
 * Is a node a primitive value.
 */
isPrimitive = \node ->
    isLiteral node && (
        node.kind === 'string'
        || node.kind === 'number'
        || node.kind === 'boolean'
        || node.kind === 'null');

/**
 * Is a node a language literal?
 */
isSimple = \node ->
    (  isLiteral node 
    || type node === 'ArrayExpression'
    || type type === 'ObjectExpression');

/**
 * Is a node plain old data?
 * 
 * Plain old data includes all primitives as well as arrays and objects, so long
 * as those object contain only pod.
 */
isPod = \node ->
    (  isPrimitive node
    || type node === 'ArrayExpression' && node.elements.every(isPod)
    || type type === 'ObjectExpression' && node.elements.every(.value \> isPod));

/* Value
 ******************************************************************************/
/**
 * Is a node a constant truthy value.
 */
isTruthy = \node ->
    (  isPrimitive node
    && !!node.value);

/* Function
 ******************************************************************************/
/**
 * Is a node a function with a block body?
 */
isBlockFunction = \node ->
    (  type node === 'FunctionExpression'
    && (
           type node.body === 'BlockStatement'
        || type node.body === 'WithStatement'
        || type node.body === 'TryStatement'));

/**
 * Is a function a lambda function? Includes functions that use the arguments
 * object.
 */
isLambda = \node ->
    (  type node === 'FunctionExpression'
    && !node.id
    && !isBlockFunction node
    && !node.params.self);

/**
 * Is a function a lambda function that does not use the arguments object?
 */
isLambdaWithoutArgs = \node ->
    (  isLambda node
    && !node.params.id);

/* User Data
 ******************************************************************************/
/**
 * Get the user data `key` or return null.
 */
tryGetUd := \def key node ->
    ?node && node.ud && node.ud.hasOwnProperty(key)
        :node.ud.(key)
        :def;

/**
 * Get the user data `key` or return null.
 */
getUd := tryGetUd @ null;

/**
 * Set the user data for `key` to `value` on `node`.
 */
setUd := \key value node ->
    setData(node, key, value);

/* Ops
 ******************************************************************************/
modify := \f node ->
    modifyAstNode(node, f);

/* Khepri User Data
 ******************************************************************************/
getUid := getUd @ 'uid';
setUid := setUd @ 'uid';

getLocals := tryGetUd @ [] @ 'locals';
setLocals := setUd @ 'locals';

getClosure := tryGetUd @ [] @ 'closure';
setClosure := setUd @ 'closure';
    
}