/**
 * @fileOverview Khepri AST nodes predicates.
 */
package (
    type
        
    isIdentifier
    
    isLiteral
    isNumberish
    isPrimitive
    isSimple
    isPod
    
    isTruthy
    
    isBlockFunction
    isLambda
    
    getUd
    getUid)
{

/**
 * Safe node type getter
 */
type = \node ->
    (node && node.type);

/* Identifier
 ******************************************************************************/
/**
 * Is a node an identifier?
 */
isIdentifier = \node ->
    (type node === 'Identifier');

/* Literal
 ******************************************************************************/
/**
 * Is a node a literal.
 * 
 * This includes regular expressions.
 */
isLiteral = \node ->
    (type node === 'Literal');

/**
 * Can a node be treated as a number?
 */
isNumberish = \node ->
    (  isPrimitive node
    && !isNaN node.value);

/**
 * Is a node a primitive value.
 */
isPrimitive = \node ->
    isLiteral node && (
        node.kind === 'string'
        || node.kind === 'number'
        || node.kind === 'boolean'
        || node.kind === 'null');

/**
 * Is a node a language literal?
 */
isSimple = \node ->
    (  isLiteral node 
    || node.type === 'ArrayExpression'
    || node.type === 'ObjectExpression');

/**
 * Is a node plain old data?
 * 
 * Plain old data includes all primitives as well as arrays and objects, so long
 * as those object contain only pod.
 */
isPod = \node ->
    (  isPrimitive node
    || node.type === 'ArrayExpression' && node.elements.every(isPod)
    || node.type === 'ObjectExpression' && node.elements.every(\x -> isPod(x.value)));

/* Value
 ******************************************************************************/
/**
 * Is a node a constant truthy value.
 */
isTruthy = \node ->
    (  isPrimitive node
    && !!node.value);

/* Function
 ******************************************************************************/
/**
 * Is a node a function with a block body?
 */
isBlockFunction = \node ->
    (  type node=== 'FunctionExpression'
    && type node.body === 'BlockStatement');

/**
 * Is a function a lambda function?
 */
isLambda = \node ->
    (  type node === 'FunctionExpression'
    && !node.id
    && !isBlockFunction node
    && !node.params.self
    && !node.params.id);

/* UID
 ******************************************************************************/
/**
 * Get some user data or return null.
 */
getUd = \name node ->
    (node && node.ud && node.ud.(name));

/**
 * Get the uid of a node or null.
 */
getUid = getUd @ 'uid';

}