/**
 * @fileOverview Pre-lexical check tree normalization.
 * 
 * This stage expands shorthand like the array unpack, but does not move or
 * remove nodes.
 */
package (
    normalize)
with
    import 'khepri-ast::node' {modify setData getData},
    import 'khepri-ast::expression' ast_expression,
    import 'khepri-ast::pattern' ast_pattern,
    import 'khepri-ast::package' ast_package,
    import 'khepri-ast::value' ast_value,
    
    import '../pseudo/expression' {CheckedMemberExpression},
    import '../pseudo/pattern' {SliceUnpack RelativeUnpack},
    
    import '../ast' {
        type
        getUd
        setUd},
    
    import '../fun' {
        constant
        concat
        map
        foldl
        foldr
        flatten},
    import '../rewriter' {
        UP
        DOWN
        Rewriter
        rewrite}
in {

var string := ast_value.Literal.create @ (null, 'string');
var number := ast_value.Literal.create @ (null, 'number');

var markReserved := setUd@('reserved', true);

/**
 * Split a list of unpacks around an ellipsis.
 */
var splitUnpackList := \elements -> let
    indx = elements.map(type).indexOf('EllipsisPattern')
in
    ?indx < 0
        :[elements, null, []]
        :[elements.slice(0, indx), elements.(indx), elements.slice(indx + 1)];

/**
 * Rejoin a split argument list into slice and relative unpacks.
 */
var createUnpackList := \pre mid post ->
    concat(
        pre,
        ?mid && mid.id
            :SliceUnpack.create(null, mid.id, null, pre.length, post.length)
            :[],
        map(
            \x i -> RelativeUnpack.create(null, x, null, post.length - i, post.length + pre.length),
            post));


var always := \_ -> true;

var rewrites := new Rewriter();

/* Package Normalizations
 ******************************************************************************/
/**
 * Add aliases to all package exports
 */
rewrites.add('PackageExport', UP,
    (!) <\ (.alias),
    
    \{id loc} ->
        ast_package.PackageExport.create(loc,
            id,
            string id.name));

/* Expression Expansions
 ******************************************************************************/
/**
 * Convert multiple binding let expression to nested single binding let expressions.
 */
rewrites.add('LetExpression', UP,
    \node ->
        node.bindings.length > 1,
    
    \{bindings body} ->
        foldr(
            \p c ->
                ast_expression.LetExpression.create(null, [c], p),
            body,
            bindings));

/**
 * Expand currying with multiple args to nested single arg curries.
 */
rewrites.add('CurryExpression', DOWN,
    \node ->
        node.args.length > 1,
    
    \{base args} ->
        foldl(
            \p arg -> 
                ast_expression.CurryExpression.create(null, p, [arg]),
            base,
            args));

/* Pattern Normalizations
 ******************************************************************************/
var markChecked := \x ->
    ?type x === 'ObjectPattern' || type x === 'ArrayPattern'
        :modify(x, {}, { checked: true })
    :?type x === 'AsPattern'
        :modify(x, { target: markChecked x.target })
    :x;

/**
 * 1) Convert array pattern to object pattern.
 * 2) Mark all subpatterns as checked.
 * 3) Generate slice and relative unpacks if ellipsis pattern used.
 */
rewrites.add('ArrayPattern', DOWN,
    \_ -> true,
    
    \{loc elements checked} -> let
        elements = ?checked : map(markChecked, elements) : elements,
        
        [pre mid post] = splitUnpackList elements
    in
        ast_pattern.ObjectPattern.create(loc,
            createUnpackList(
                map(
                    \x i -> ast_pattern.ObjectPatternElement.create(null, number i, x),
                    pre),
                mid,
                post),
            checked));

/**
 * Mark all subpatterns as checked.
 */
rewrites.add('ObjectPattern', DOWN,
    (.checked),
    
    \node ->
        modify @ node {
            elements: map(
                \element ->
                    modify(element, { target: markChecked element.target, key: markChecked element.key }),
                node.elements)
        });

/**
 * Rewrite arg pattern elements if an ellipsis pattern is used.
 * 
 * Also ensure arguments patterns with an ellipsis have an argument name.
 */
rewrites.add('ArgumentsPattern', UP,
    \node ->
       node.elements.map(type).indexOf('EllipsisPattern') >= 0,
    
    let
        __args = markReserved <|
            ast_pattern.IdentifierPattern.create(null,
                ast_value.Identifier.create(null, '__args'))
    in
         \node -> let
            [pre mid post] = splitUnpackList node.elements
        in
            modify @ node {
                id: (node.id || __args),
                elements: createUnpackList(pre, mid, post)
            });

/**
 * Normalize object patterns to ('key': pattern) form.
 */
rewrites.add('ObjectPatternElement', DOWN,
    (!) <\ (.target),
    
    \node#{key} -> {
        switch (type key) {
        case 'IdentifierPattern':
            return ast_pattern.ObjectPatternElement.create(node.loc,
                string(key.id.name),
                key);
        
        case 'AsPattern':
            return ast_pattern.ObjectPatternElement.create(node.loc,
                string(key.id.id.name),
                key);
        
        default:
            return node;
        }
    });

/**
 * When generating unpacks lists, we want to use the 
 * as pattern name if possible instead of the auto generated name.
 */
rewrites.add('AsPattern', DOWN,
    (!) <\ ((.target) \> getUd @ 'id'),
    
    \node ->
        ast_pattern.AsPattern.create(null,
            node.id,
            setUd('id', node.id, node.target)));

/**
 * Name anon object patterns.
 * 
 * This uses the identifier `__o`, which is marked reserved so the lexical check
 * does not complain if it is not unique.
 */
rewrites.add('ObjectPattern', UP,
    (!) <\ getUd @ 'id',
    
    let
        __o = markReserved <|
            ast_pattern.IdentifierPattern.create(null,
                ast_value.Identifier.create(null, '__o'))
    in
        \node ->
            ast_pattern.AsPattern.create(null,
                __o,
                setUd('id', __o, node)));

/**
 * Rewrite sink patterns to identifier patterns.
 */
rewrites.add('SinkPattern', DOWN,
    always,
    
    constant <| markReserved <|
        ast_pattern.IdentifierPattern.create(null,
            ast_value.Identifier.create(null, '_')));

/* Interface
 ******************************************************************************/
/**
 * Pre normalize a Khepri AST.
 * 
 * @param ast Zippered Khepri AST.
 */
normalize = rewrite @ rewrites;

}