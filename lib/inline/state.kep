/**
 * @fileOverview Inlining state record.
 * 
 * @TODO: split into inlining scope and state.
 */
package (
    State
    
    getBinding
    
    push
    pop
    
    getGlobals
    addGlobal)
with
    import 'bes::record' record,

    import 'hamt' hamt,
    
    import './binding' {
        Binding
        MUTABLE
        IMMUTABLE}
in {

/* Record
 ******************************************************************************/
/**
 * Inlining state record.
 * 
 * @member bindings Map of unique ids to immutable bindings.
 * @member bindings Map of unique ids to working values.
 * @member globals List of globals.
 * @member outer Outer state.
 */
State := record.declare(null, [
    'bindings',
    'working',
    'globals',
    'outer']);

State.empty = new State(
    hamt.empty,
    hamt.empty,
    hamt.empty,
    null);

/**
 * 
 */
State.prototype.addBinding = \uid target simple =s->
    s.setBindings(
        hamt.set(
            uid,
            Binding.create(target, IMMUTABLE, simple),
            s.bindings));

/**
 * 
 */
State.prototype.addWorking = \uid target simple =s->
    s.setWorking(
        hamt.set(
            uid,
            Binding.create(target, MUTABLE, simple),
            s.working));

/* Scope Operations
 ******************************************************************************/
/**
 * Enter an new scope.
 * 
 * @param s State.
 */
push = \s ->
    s
        .setOuter(s)
        .setWorking(hamt.empty);

/**
 * Exit the current scope.
 * 
 * @param s State.
 */
pop = let
    nullWorking =
        hamt.fold @ \p {key} ->
            hamt.set(key, null, p)
in
    \s ->
        s.outer
            .setBindings(s.bindings)
            .setGlobals(s.globals)
            .setWorking(
                // Delete working bindings modified in inner from outer scope.
                nullWorking(s.outer.working, s.working));

/* Binding Operations
 ******************************************************************************/
/**
 * 
 */
getBinding := \uid s -> 
    (  hamt.get(uid, s.bindings)
    || hamt.get(uid, s.working));

/* Globals Operations
 ******************************************************************************/
/**
 * Get list of global symbols.
 * 
 * @param s State.
 */
getGlobals := (.globals) \> hamt.keys;

/**
 * Register a new global.
 * 
 * @param name Global symbol name.
 * @param s State.
 */
addGlobal := \name s ->
    s.setGlobals(
        hamt.set(name, name, s.globals));

}