package (
    markExpansion
    incrementCount
    
    getExpansion
    isExpansion
    mergeExpansions
    expandNode)
with
    import 'bes::record' record,
    
    import 'khepri-ast::node' {
        setData},
    
    import '../ast' {
        getUid
        getUd}
in {

var Expansion := record.declare(null, [
    'count',
    'value']);

var MAX_EXPANSION_DEPTH := 1;

var canExpand := \exp -> 
    exp.count < MAX_EXPANSION_DEPTH;

var setExpansion := \node expansion ->
    setData(node, 'expand', expansion);

/**
 * Get the expansion for `node`.
 * 
 * @param node AST node.
 */
getExpansion := getUd @ 'expand';

/**
 * Is `node` an expansion?
 * 
 * @param node AST node.
 */
isExpansion := getExpansion;

/**
 * Mark `node` as an expansion.
 * 
 * @param node AST node.
 * @param count Number of times node has been expanded.
 * @param target Expansion value.
 */
markExpansion := \node count value ->
    ?isExpansion value
        :setExpansion(node, getExpansion value)
        :setExpansion(
            node,
            Expansion.create(
                Math.max(
                    count,
                    ?isExpansion node : getExpansion(node).count : 0),
                value));

mergeExpansions := \val other ->
    ?isExpansion other && isExpansion val
        :setExpansion(
            val,
            Expansion.create(
                Math.max(
                    getExpansion(val).count,
                    getExpansion(other).count)))
        :val;

/**
 * 
 */
incrementCount := \node count value -> let
    exp = getExpansion node
in
    markExpansion(
        node,
         ((exp && exp.count) || count) + 1,
         value);

/**
 * Try to expand `node`.
 */
expandNode := \node ->
    ?isExpansion node
        :let exp = getExpansion node in
            ?canExpand exp
                :exp.value
                :setExpansion(node, null)
        :node;

}