/**
 * @fileOverview Pre-lexical check tree normalization.
 * 
 * This stage expands shorthand like the array unpack, but does not move or
 * remove nodes.
 */
package (
    normalize)
with
    import 'khepri-ast-zipper' {khepriZipper},
    
    import 'khepri-ast::node' {setData},
    import 'khepri-ast::expression' ast_expression,
    import 'khepri-ast::pattern' ast_pattern,
    import 'khepri-ast::package' ast_package,
    import 'khepri-ast::value' ast_value,
    
    import './fun' {
        map
        foldl
        foldr},
    import './rewriter' {
        UP
        DOWN
        Rewriter
        rewrite}
in {

var string = ast_value.Literal.create @ (null, 'string');


var peepholes = new Rewriter();

/* Package Normalizations
 ******************************************************************************/
/**
 * Add aliases to all package exports
 */
peepholes.add('PackageExport', UP,
    \node ->
        !node.alias,
    
    \node ->
        ast_package.PackageExport.create(node.loc,
            node.id,
            string(node.id.name)));

/* Expression Expansions
 ******************************************************************************/
/**
 * Convert multiple binding let expression to nested single binding let expressions
 */
peepholes.add('LetExpression', UP,
    \node ->
        node.bindings.length > 1,
    
    \{bindings body} ->
        foldr(
            \p c ->
                ast_expression.LetExpression.create(null, [c], p),
            body,
            bindings));

/**
 * Expand currying with multiple args to nested single arg curries curries.
 */
peepholes.add('CurryExpression', DOWN,
    \node ->
        node.args.length > 1,
    
    \node ->
        foldl(
            \p arg -> 
                ast_expression.CurryExpression.create(null, p, [arg]),
            node.base,
            node.args));

/* Pattern Normalizations
 ******************************************************************************/
/**
 * Convert array patterns to object patterns
 */
peepholes.add('ArrayPattern', DOWN,
    \_ -> true,
    \node#{loc elements} ->
        ast_pattern.ObjectPattern.create(loc,
            map(
                \x i ->
                    ast_pattern.ObjectPatternElement.create(null,
                        string(i + ''),
                        x),
                elements)));

/**
 * Normalize object patterns to ('key': pattern) form.
 */
peepholes.add('ObjectPatternElement', DOWN,
    \node ->
        !node.target,
    
    \node#{key} -> {
        switch(key.type) {
        case 'IdentifierPattern':
            return ast_pattern.ObjectPatternElement.create(node.loc,
                string(key.id.name),
                key);
        
        case 'AsPattern':
            return ast_pattern.ObjectPatternElement.create(node.loc,
                string(key.id.id.name),
                key);
        
        default:
            return node;
        }
    });

/**
 * When generating unpacks lists, we want to use the 
 * as pattern name if possible instead of the auto generated name.
 */
peepholes.add('AsPattern', DOWN,
    \node ->
        !node.target.ud || !node.target.ud.id,
    
    \node ->
        ast_pattern.AsPattern.create(node.loc,
            node.id,
            setData(node.target, 'id', node.id)));

/**
 * Name anon object patterns.
 * 
 * This uses the identifier `__o`, which is marked reserved so the lexical check
 * does not complain if it is not unique.
 */
peepholes.add('ObjectPattern', UP,
    \node ->
        !node.ud || !node.ud.id,
    
    \node -> let
        id = setData(
            ast_pattern.IdentifierPattern.create(null,
                ast_value.Identifier.create(null, '__o')),
            'reserved',
            true)
    in
        ast_pattern.AsPattern.create(null,
            id,
            setData(node, 'id', id)));

/* Interface
 ******************************************************************************/
normalize = khepriZipper \> rewrite @ peepholes;

}