/**
 * @fileOverview 
 */
package (
    Scope
    
    addUid
    addBinding
    addMutableBinding
    addImmutableBinding
    setBindingMutability
    addMapping
    
    push
    pop)
with
    import 'bes::record' record,
    
    import 'hashtrie' hashtrie
in {

var IMMUTABLE := 0,
    MUTABLE := 1;

/* Scope
 ******************************************************************************/
/**
 * Holds information about a lexical scope.
 * 
 * @param record Maps identifiers to binding information for this scope.
 * @param outer Parent scope.
 * @param mapping Maps identifiers to their actual names in this scope.
 */
Scope = record.declare(null, [
    'record',
    'outer',
    'mapping',
    'definitions']);

Scope.empty = Scope.create(hashtrie.empty, null, hashtrie.empty, hashtrie.empty);

/**
 * Does this scope have a binding for `id`?
 */
Scope.prototype.hasOwnBinding = \id =self->
    hashtrie.has(id, self.record);

/**
 * Does this scope or its parents have a given binding for `id`?
 */
Scope.prototype.hasBinding = \id =self->
    (  self.hasOwnBinding(id)
    || (self.outer && self.outer.hasBinding(id)));

/**
 * Does this scope or its parents have a mutable binding for `id`?
 */
Scope.prototype.hasMutableBinding = \id =self-> let
    binding = self.getBinding(id)
in
    (binding && binding.mutable);

/**
 * Get information for a given binding. Returns null if binding does not exist.
 */
Scope.prototype.getBinding = \id =self->
    (  hashtrie.get(id, self.record)
    || ?self.outer
        :self.outer.getBinding(id)
        :null);

/**
 * Get the unique identifier for `id`.
 */
Scope.prototype.getUid = \id =self->
    ?self.hasOwnBinding(id)
        :hashtrie.get(id, self.definitions)
        :?self.outer
            :self.outer.getUid(id)
            :null;

/**
 * Does this scope have its own a mapping for a given identifier.
 */
Scope.prototype.hasOwnMapping = \id =self->
    hashtrie.has(id, self.mapping);

/**
 * Does this scope have any mapping for a given identifier.
 */
Scope.prototype.hasMapping = \id =self->
    (  self.hasOwnMapping(id)
    || (self.outer && self.outer.hasMapping(id)));

/**
 * Get the mapped value for a given identifier.
 */
Scope.prototype.getMapping = \id =self->
    ?self.hasOwnMapping(id)
        :hashtrie.get(id, self.mapping)
        :self.outer && self.outer.getMapping(id);

/**
 * Get an unused identifier in this scope.
 */
Scope.prototype.getUnusedId = \id =self-> {
    if (!self.hasBinding(id))
        return id;
    
    for (var i = 0; ; i = i + 1)
        if (!self.hasBinding(id + i))
            return id + i;
};

/* Operations
 ******************************************************************************/
/**
 * Register a unique identifier in scope `s`.
 * 
 * @param s Scope.
 * @param id Identifier
 * @param uid Unique identifier.
 */
addUid = \s id uid->
    s.setDefinitions <|
        hashtrie.set(id, uid, s.definitions);

/**
 * Create a new modified scope with a given binding.
 * 
 * @param s Scope.
 * @param id Identifier for new binding.
 * @param info Binding information.
 */
addBinding = \s, id, info -> 
    s.setRecord <| 
        hashtrie.set(id, info, s.record);

/**
 * Create a new modified scope with a new mutable binding.
 * 
 * @s Scope
 * @param id Identifier for new binding
 * @param loc Location of binding in source code.
 */
addMutableBinding = \s, id, loc ->
    addBinding(s, id, {
        'mutable': MUTABLE,
        'loc': loc
    });

/**
 * Create a new modified scope with a new immutable binding.
 * 
 * @s Scope
 * @param id Identifier for new binding
 * @param loc Location of binding in source code.
 */
addImmutableBinding = \s, id, loc ->
    addBinding(s, id, {
        'mutable': IMMUTABLE,
        'loc': loc
    });

/**
 * Create a new modified scope with a new immutable binding.
 * 
 * @s Scope
 * @param id Identifier for new binding
 * @param loc Location of binding in source code.
 */
setBindingMutability = \s id mutable ->
    ?s.hasOwnBinding id
        :s.setRecord <|
            hashtrie.modify(id, \binding -> ({
                'loc': binding.loc,
                'mutable': ?mutable : MUTABLE : IMMUTABLE
            }), s.record)
        :(s.outer && s.setOuter(setBindingMutability(s.outer, id, mutable)));

/**
 * Create a new modified scope with a new mapping
 * 
 * @s Scope
 * @param from Key value.
 * @param to Value to map to.
 */
addMapping = \s from to ->
    s.setMapping(
        hashtrie.set(from, to, s.mapping));

/* Operations
 ******************************************************************************/
push = \s ->
    Scope.empty
        .setOuter(s)
        .setDefinitions(s.definitions);

pop = \s -> s.outer;

}