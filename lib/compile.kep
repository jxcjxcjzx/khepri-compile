/**
 * @fileOverview Main khepri compiler function.
 * 
 * Translates Khepri source to ECMAScript.
 * 
 * TODO: Use Error monad for each stage instead of direct composition with 
 * throwing functions.
 */
package compile
with
    import 'akh::error' Error,
    import 'akh::base' {sequence},

    import './stages/pre_normalize' pre_normalize,
    import './stages/lexical' lexical,
    import './stages/post_normalize' post_normalize,
    import './stages/inline' inline,
    import './stages/khepri_peep' khepri_peep,
    import './stages/transform' transform,
    import './stages/ecma_peep' ecma_peep
in {

var compiler := \x ->
    Error.tryError(
        pre_normalize(x)
            .chain(lexical)
            .chain(post_normalize)
            .chain(inline)
            .chain(khepri_peep)
            .chain(transform)
            .chain(ecma_peep),
        \x -> { throw x; });

var extract := \{tree} -> tree;


/**
 * Translate a Khepri AST to an ECMAScript AST.
 * 
 * @param root Khepri ast.
 * @param [options] Map of options sepecifying behavior of compiler.
 */
compile =
    (\root options -> ({
        'tree': root,
        'options': options || {}}))
   \>> compiler
   \> extract;

}